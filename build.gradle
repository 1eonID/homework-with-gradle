subprojects {
  apply plugin: 'java'
  apply plugin: 'checkstyle'

  repositories {
      jcenter()
  }

  dependencies {
      testCompile 'junit:junit:4.12'
      testCompile 'org.hamcrest:hamcrest-all:1.3'
  }

  sourceSets {
    acceptanceTests {
      java {
        srcDir file('../acceptanceTests/src')
        compileClasspath += main.output
        runtimeClasspath += main.output
      }
    }
  }

  configurations {
    acceptanceTestsCompile.extendsFrom testCompile
    acceptanceTestsRuntime.extendsFrom testRuntime
  }

  task acceptanceTests(type: Test) {
    testClassesDir = project.sourceSets.acceptanceTests.output.classesDir
    classpath = project.sourceSets.acceptanceTests.runtimeClasspath
  }

  checkstyle {
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    checkstyleTest.enabled = false
    checkstyleAcceptanceTests.enabled = false
  }
}
